plugins {
    id 'java'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '8.1.0'
    id 'com.gradleup.shadow' version '9.3.0'
}

group = project.group

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
}

repositories {
    mavenCentral()
}

dependencies {
    // Hytale server
    // compileOnly fileTree(dir: "$rootDir/libs", include: ["*.jar"])
    // Needed libs
    implementation 'com.h2database:h2:2.4.240'
    implementation 'com.zaxxer:HikariCP:7.0.2'
    implementation 'org.slf4j:slf4j-simple:2.0.17'
    implementation 'org.yaml:snakeyaml:2.5'
    implementation 'net.objecthunter:exp4j:0.4.8'
    // Drivers
    implementation 'com.mysql:mysql-connector-j:9.5.0'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.5.7'
    implementation 'org.postgresql:postgresql:42.7.8'
    // Testing
    testImplementation platform("org.junit:junit-bom:6.0.1")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
    useJUnitPlatform()
}

spotless {
    java {
        eclipse().configFile("$rootDir/eclipse-formatter.xml")
        endWithNewline()
        importOrder("", "java", group.toString(), "\\#")
        leadingTabsToSpaces(4)
        removeUnusedImports()
        trimTrailingWhitespace()
    }
}

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()

    manifest {
        attributes(
                'Main-Class': 'com.azuredoom.levelingcore.LevelingCore'
        )
    }
}

tasks.build {
    dependsOn("spotlessApply", "shadowJar")
}

publishing {
    repositories {
        maven {
            name = project.mod_id
            url = project.maven_url
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
}

def keyPropsFile = new File(rootProject.projectDir, "key.properties")

if (keyPropsFile.exists()) {
    tasks.register("publishToModtale", Exec) {
        group = "publishing"
        description = "Publishes the built jar and changelog to Modtale.net."
        dependsOn tasks.named("build")
        def curlExe = org.gradle.internal.os.OperatingSystem.current().isWindows()
                ? "C:/Windows/System32/curl.exe"
                : "curl"

        doFirst {
            def releaseProp = new Properties()
            releaseProp.load(new FileInputStream(keyPropsFile))
            def apiKey = releaseProp.getProperty("modTaleKey")
            def projectId = releaseProp.getProperty("modTaleProjectId")
            def jarFile = layout.buildDirectory
                    .file("libs/${project.name}-${project.version}.jar")
                    .get().asFile
            def changelogFile = rootProject.file("changelog.md")
            def changelogText = changelogFile.text

            commandLine(
                    curlExe, '-X', 'POST',
                    "https://modtale.net/api/projects/${projectId}/versions",
                    '-H', "X-MODTALE-KEY: ${apiKey}",
                    '-F', "file=@${jarFile.absolutePath}",
                    '-F', "versionNumber=${project.version}",
                    '-F', "channel=${project.release_type}",
                    '-F', "gameVersions=${project.hytale_version}",
                    '-F', "changelog=${changelogText}"
            )
        }
    }
}